<alpha>			::= [a-z] | [A-Z]
<digit>			::= [0-9]
<hexdig>		::= <digit> | ("A" | "B" | "C" | "D" | "E" | "F")
<u32>			::= <digit>+ # valid rust u32
<f64>			::= ("+" | "-")? <digit>+  ( "." <digit>+ )? # valid rust f64
<bool>			::= "0" | "1"
<image_src>		::= ( <alpha> | <digit> | "_" )+ ( ".png" | ".jpg") # valid image resource link
<track_id>		::= <u32>
<train_id>		::= <u32>
<node_id>		::= <u32>
<track_count>	::= <u32>
<color>			::= "#" <hexdig> <hexdig> <hexdig> <hexdig> <hexdig> <hexdig>
<thickness>		::= <f64>
<start_t>		::= <f64>
<duration>		::= <f64>
<coord>			::= <f64> ";" <f64>
<direction>		::= "forward" | "backward"

<pressed_ctrl>	::= <bool>
<pressed_shift>	::= <bool>
<pressed_alt>	::= <bool>
<modifier>		::= <pressed_ctrl> "," <pressed_shift> "," <pressed_alt>
<click>			::= "click\n" <train_id> " " <modifier>
<client_packet>	::= <click>

<bezier2>		::= "bezier2;" <coord> ";" <coord>
<bezier3>		::= "bezier3;" <coord> ";" <coord> ";" <coord>
<bezier4>		::= "bezier4;" <coord> ";" <coord> ";" <coord> ";" <coord>
<bezier>		::= <bezier2> | <bezier3> | <bezier4>
<track_update>	::= "track\n" <track_count> ( "\n" <track_id> " " <bezier> " " <color> " " <thickness>)+
<train_update>	::= "train\n" <train_id> " " <track_id> " " <start_t> " " <duration> " " <direction> "\n" <image_src>
<node_update>	::= "node\n" <node_id> " " <coord>
<removal_type>	::= "explosion" | "silent" | "derail" | "vibrate" | "take_off"
<train_remove>	::= "remove\n" <train_id> " " <removal_type>
<server_packet>	::= <track_update> | <train_update> | <node_update> | <train_remove>

# <ctrl_click>	::= "ctrl_click\n" (("train " <train_id>) | ("track " <track_id>) | ("node" <node_id>))
<node_new>		::= "node_new\n" <coord> " " <node_type>
<train_speed>	::= <f64>
<train_new>		::= "train_new\n" <track_id> " " <train_speed>
<track_new>     ::= "track_new\n" <node_id> " " <node_id> " " <color>
<node_type>		::= "random" | "roundrobin" | "reverse" | "derail" | "configurable"
<node_move>		::= "node_move\n" <node_id> " " <coord>
<node_edit>		::= "node_edit\n" <node_id> " " <node_type>
<node_delete>	::= "node_delete\n" <node_id>
<track_adjust>	::= "track_adjust\n" <track_id>  " " ( <coord> | <coord> "," <coord>)?
<track_delete>	::= "track_delete\n" <track_id>
<ctrl_packet>	::=	<node_new> | <train_new> | <track_new> | <node_move> | <track_adjust>
