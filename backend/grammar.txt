<alpha>        	::= [a-z] | [A-Z]
<digit>        	::= [0-9]
<hexdig>       	::= <digit> | ("A" | "B" | "C" | "D" | "E" | "F")
<u32>           ::= <digit>+ # valid rust u32
<f64>           ::= ("+" | "-")? <digit>+  ( "." <digit>+ )? # valid rust f64
<image_src>     ::= ( <alpha> | <digit> | "_" )+ ( ".png" | ".jpg") # valid image resource link
<track_id>		::= <u32>
<train_id>		::= <u32>
<track_count>   ::= <u32>
<color>			::= "#" <hexdig> <hexdig> <hexdig> <hexdig> <hexdig> <hexdig>
<thickness>		::= <f64>
<start_t>	    ::= <f64>
<duration>		::= <f64>
<coord>			::= <f64> "," <f64>
<bezier>		::= <bezier2> | <bezier3> | <bezier4>
<bezier2>	    ::= "bezier2;" <coord> ";" <coord>
<bezier3>	    ::= "bezier3;" <coord> ";" <coord> ";" <coord>
<bezier4>	    ::= "bezier4;" <coord> ";" <coord> ";" <coord> ";" <coord>
<track_update>	::= "track\n" <track_count> ( "\n" <track_id> " " <bezier> " " <color> " " <thickness>)+ # redraw and update train track list
<train_update>	::= "train\n" <train_id> " " <track_id> " " <start_t> " " <duration> "\n" <image_src> # start drawing train on certain track with certain image, lasting duration secord in total
<click>			::= "click\n" <train_id> # train got clicked on client side
<server_packet>	::= <track_update> | <train_update>
<client_packet>	::= <click>
